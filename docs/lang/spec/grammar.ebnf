(* Raven language EBNF grammar
   This grammar captures the structural form of Raven source code.
   Contextual rules and parsing details are described in the language specification.
   NOTE: Non-normative. Context-sensitive parsing and validation live outside this EBNF. *)

CompilationUnit          ::= {ImportDirective} {AliasDirective} {Declaration | Statement} EOF ;

ImportDirective          ::= 'import' QualifiedName ('.' '*')? ; (* Namespace imports require '.*'; applying '.*' to a type imports its static members and nested types *)
AliasDirective           ::= 'alias' Identifier '=' Type ; (* Target may be a fully qualified name, type expression, literal type, or predefined type: bool | char | int | string | unit | '()' *)

(* ---------- Modifiers ---------- *)

MemberModifiers          ::= { MemberModifier } ;
MemberModifier           ::= AccessModifier
                           | 'static' | 'abstract' | 'virtual' | 'override' | 'sealed'
                           | 'readonly' | 'async' | 'extern' ;

AccessModifier           ::= 'public' | 'internal' | 'protected' | 'private' ;

AccessorModifier         ::= AccessModifier ;

TypeModifiers            ::= { TypeModifier } ;
TypeModifier             ::= AccessModifier | 'abstract' | 'sealed' | 'open' | 'partial' | 'static' ;

FunctionModifiers        ::= { FunctionModifier } ;
FunctionModifier         ::= AccessModifier | 'async' | 'extern' | 'static' ;

(* ---------- Declarations ---------- *)

Declaration              ::= NamespaceDeclaration
                           | FunctionDeclaration
                           | EnumDeclaration
                           | ClassDeclaration
                           | StructDeclaration
                           | InterfaceDeclaration ;

NamespaceDeclaration     ::= 'namespace' Identifier '{' {ImportDirective} {AliasDirective} {Declaration | Statement} '}' ;

FunctionDeclaration      ::= FunctionModifiers?
                             'func' Identifier '(' ParameterList? ')'
                             ReturnTypeClause?
                             ( Block | '=>' Expression ) ;

ReturnTypeClause         ::= '->' Type ;

ParameterList            ::= Parameter {',' Parameter} ;
Parameter                ::= ['out'] Identifier ':' Type ['=' Expression] ;

EnumDeclaration          ::= TypeModifiers?
                             'enum' Identifier '{' EnumMembers? '}' ;
EnumMembers              ::= EnumMember {',' EnumMember} ;
EnumMember               ::= Identifier ['=' Expression] ;

ClassDeclaration         ::= TypeModifiers? 'class' Identifier PrimaryConstructor? BaseList? ClassBody ;
PrimaryConstructor       ::= '(' ParameterList? ')' ;
StructDeclaration        ::= TypeModifiers? 'struct' Identifier ClassBody ;
InterfaceDeclaration     ::= TypeModifiers? 'interface' Identifier BaseList? ClassBody ;
BaseList                 ::= ':' Type {',' Type} ;
ClassBody                ::= '{' {ClassMember} '}' ;

ClassMember              ::= FieldDeclaration
                           | MethodDeclaration
                           | InvocationOperatorDeclaration
                           | ConstructorDeclaration
                           | PropertyDeclaration
                           | IndexerDeclaration ;

FieldDeclaration         ::= MemberModifiers? FieldKind VariableDeclarators ';' ;
FieldKind                ::= 'let' | 'var' ;

VariableDeclarators      ::= VariableDeclarator {',' VariableDeclarator} ;
VariableDeclarator       ::= Identifier (':' Type)? ['=' Expression] ;

ConstructorDeclaration   ::= MemberModifiers?
                             'init' Identifier? '(' ParameterList? ')'
                             ( Block | '=>' Expression ) ;

MethodDeclaration        ::= MemberModifiers?
                             ExplicitInterfaceSpecifier? Identifier '(' ParameterList? ')'
                             ReturnTypeClause?
                             ( Block | '=>' Expression ) ;

ExplicitInterfaceSpecifier ::= Type '.' ;

InvocationOperatorDeclaration
                           ::= MemberModifiers?
                               'self' '(' ParameterList? ')'
                               ReturnTypeClause?
                               ( Block | '=>' Expression ) ;

PropertyDeclaration      ::= MemberModifiers?
                             ExplicitInterfaceSpecifier? Identifier ':' Type AccessorList ;

IndexerDeclaration       ::= MemberModifiers?
                             ExplicitInterfaceSpecifier? Identifier BracketedParameterList ':' Type AccessorList ;

AccessorList             ::= '{' Accessor {Accessor} '}' ;
Accessor                 ::= AccessorModifier? ('get' | 'set')
                             ( Block | '=>' Expression | ';' ) ;

BracketedParameterList   ::= '[' ParameterList? ']' ;

(* ---------- Statements ---------- *)

(* Newline tokens act as implicit statement terminators unless the parser is
   in a continuation context that still requires more of the current
   expression—such as immediately after '=' or a binary operator. In those
   cases the newline is preserved as trivia on the next token instead of
   ending the statement. Other terminators, including ';', '}', and keywords
   like 'else' that conclude the enclosing construct, serve the same role when
   they appear. *)

Statement                ::= LocalDeclaration
                           | ReturnStatement
                           | BreakStatement
                           | ContinueStatement
                           | FunctionStatement
                           | IfStatement
                           | WhileStatement
                           | ForStatement
                           | ExpressionStatement ;

LocalDeclaration         ::= ('let' | 'var') LocalVariableDeclarators ;
LocalVariableDeclarators ::= LocalVariableDeclarator {',' LocalVariableDeclarator} ;
LocalVariableDeclarator  ::= Identifier (':' Type)? '=' Expression ;

ReturnStatement          ::= 'return' Expression? ;

BreakStatement           ::= 'break' ;

ContinueStatement        ::= 'continue' ;

FunctionStatement        ::= 'func' Identifier '(' ParameterList? ')' ReturnTypeClause? Block ;

ExpressionStatement      ::= Expression ;

IfStatement              ::= 'if' Expression Statement ['else' Statement] ;
WhileStatement           ::= 'while' Expression Statement ;
ForStatement             ::= 'for' 'each'? Identifier 'in' Expression Statement ;

(* ---------- Expressions & precedence (lowest → highest) ---------- *)

Expression               ::= AssignmentExpression ;

AssignmentExpression     ::= AssignmentTarget AssignmentOperator AssignmentExpression
                           | ConditionalNullCoalesceExpression ;

AssignmentOperator       ::= '=' | '+=' | '-=' | '*=' | '/=' | '%=' ;

AssignmentTarget         ::= Assignable | Pattern ;

Assignable               ::= Identifier
                           | MemberAccessExpression
                           | MemberBindingExpression
                           | ElementAccessExpression ;

Pattern                  ::= VariablePattern | DeclarationPattern | TuplePattern ;

VariablePattern          ::= ('let' | 'var') VariableDesignation ;
DeclarationPattern       ::= Type VariableDesignation ;
TuplePattern             ::= '(' PatternElement {',' PatternElement} ')' ;
PatternElement           ::= [Identifier ':'] Pattern ;

VariableDesignation      ::= VariableDesignationCore [':' Type] ;
VariableDesignationCore  ::= Identifier | ParenthesizedVariableDesignation ;
ParenthesizedVariableDesignation
                           ::= '(' VariableDesignation {',' VariableDesignation} ')' ;

ConditionalNullCoalesceExpression
                           ::= LogicalOrExpression {'??' LogicalOrExpression} ;

LogicalOrExpression      ::= LogicalAndExpression {'||' LogicalAndExpression} ;
LogicalAndExpression     ::= EqualityExpression {'&&' EqualityExpression} ;
EqualityExpression       ::= RelationalExpression {('==' | '!=') RelationalExpression} ;
RelationalExpression     ::= TypeTestExpression
                             {('<' | '>' | '<=' | '>=') TypeTestExpression} ;

TypeTestExpression       ::= AdditiveExpression {('is' | 'as') Type} ;

AdditiveExpression       ::= MultiplicativeExpression {('+' | '-') MultiplicativeExpression} ;
MultiplicativeExpression ::= UnaryExpression {('*' | '/' | '%') UnaryExpression} ;

UnaryExpression          ::= PostfixExpression
                           | ('+' | '-' | '!') UnaryExpression
                           | CastExpression ;

CastExpression           ::= '(' Type ')' UnaryExpression ;

PostfixExpression        ::= PrimaryExpression { PostfixTrailer } ;
PostfixTrailer           ::= ArgumentList
                           | MemberAccessTrailer
                           | ElementAccessTrailer
                           | ConditionalAccessTrailer ;

ArgumentList             ::= '(' [Argument {',' Argument}] ')' ;
Argument                 ::= [Identifier ':'] Expression ;

MemberAccessTrailer      ::= '.' Identifier ;
ElementAccessTrailer     ::= '[' Expression ']' ;
ConditionalAccessTrailer ::= '?' (MemberAccessTrailer | ElementAccessTrailer) ;

MemberBindingExpression  ::= '.' Identifier ;

(* ---------- Primaries ----------
   NOTE: Block and IfExpression can appear as expressions.
         While and for constructs exist only as statements. *)
PrimaryExpression        ::= Literal
                           | Identifier
                           | MemberBindingExpression
                           | ObjectCreationExpression
                           | TupleExpression
                           | ParenthesizedExpression
                           | Block
                           | IfExpression
                           | InterpolatedStringExpression ;

Block                    ::= '{' {Statement} '}' ;

IfExpression             ::= 'if' Expression Expression 'else' Expression ;
InterpolatedStringExpression ::= '"' {InterpolatedStringContent} '"' ;
InterpolatedStringContent ::= InterpolatedStringText | Interpolation ;
InterpolatedStringText   ::= /* text segment without ${ or closing quote */ ;
Interpolation            ::= '${' Expression '}' ;

ObjectCreationExpression ::= 'new' Type '(' [Argument {',' Argument}] ')' ;

TupleExpression          ::= '(' TupleElement {',' TupleElement} ')' ;
TupleElement             ::= [Identifier ':'] Expression ;

ParenthesizedExpression  ::= '(' Expression ')' ;

Literal                  ::= NumericLiteral
                           | StringLiteral
                           | 'true' | 'false' | 'null' ;

(* ---------- Types ---------- *)

Type                     ::= '&'? UnionType ;

UnionType                ::= NullableType {'|' NullableType} ;     (* left-assoc *)

NullableType             ::= PrimaryType ['?'] ;

PrimaryType              ::= TupleType
                           | GenericType
                           | SimpleType
                           | NullType
                           | ParenthesizedType ;

NullType                 ::= 'null' ;

SimpleType               ::= BuiltinType | QualifiedName ;
BuiltinType              ::= 'int' | 'string' | 'bool' | 'unit' | 'char' | '(' ')' ;

GenericType              ::= QualifiedName '<' TypeList '>' ;
TypeList                 ::= Type {',' Type} ;

TupleType                ::= '(' TupleTypeElement {',' TupleTypeElement} ')' ;
TupleTypeElement         ::= [Identifier ':'] Type ;
ParenthesizedType        ::= '(' Type ')' ;

QualifiedName            ::= Identifier {'.' Identifier} ;

Identifier               ::= /* ASCII letter, '_' or '$' followed by letters, digits, '_' or '$'.
                               The single '_' token is reserved for discards. */ ;
NumericLiteral           ::= /* int | float | hex | bin (lexically resolved) */ ;
StringLiteral            ::= '"' /* content with escapes */ '"' ;