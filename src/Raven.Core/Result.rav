namespace System

import System.*

/// Represents either a result with a value, or an error
///
/// ## Usage
/// ```raven
/// func Test(v: int) -> Result<int> {
///     if v > 3 {
///         return .Ok(42)
///     }
///     return .Error("Wrong number")
/// }
/// ```
public union Result<T> {
    /// Represents a result value
    Ok(value: T)

    /// Represents an error
    Error(message: string)
}

/// Represents either a result with a value, or an error
///
/// ## Usage
/// ```raven
/// func Test(v: int) -> Result<int, string> {
///     if v > 3 {
///         return .Ok(42)
///     }
///     return .Error("Wrong number")
/// }
/// ```
public union Result<T, E> {
    /// Represents a result value
    Ok(value: T)

    /// Represents an error
    Error(data: E)
}

/// Contains extensions for the Result<T> type
public extension ResultExtensions<T> for Result<T> {

    /// Gets a value indicating whether the Result was Ok
    public IsOk: bool {
        get => self is .Ok(message)
    }

    /// Gets a value indicating whether the Result was Error
    public IsError: bool {
        get => self is .Error(message)
    }

    /// Unwrap the value if Ok, otherwise return the default value
    public UnwrapOrDefault() -> T {
        if self is .Ok(value) {
            return value
        }

        return default(T)
    }

    /// Unwrap the value if Ok, otherwise throw an exception
    public UnwrapOrThrow() -> T {
        var error: string = ""
        if self is .Ok(value) {
            return value
        } else if self is .Error(error2) {
            error = error2
        }

        throw new InvalidOperationException(error)
    }

    /// Unwrap the value if Ok, otherwise return the specified value
    public UnwrapOr(defaultValue: T) -> T {
        if self is .Ok(value) {
            return value
        }

        return defaultValue
    }

    /// Unwrap the Error, or throw an exception
    public UnwrapError() -> string {
        if self is .Error(message) {
            return message
        }

        throw new InvalidOperationException("Result is not Error")
    }
}
