namespace System

import System.*

/// Represents either a result with a value, or an error. See also: [Result&lt;T&gt;](xref:T:System.Result`1).
///
/// ## Usage
/// Use `Result` like so:
///
/// ```raven
/// func Test(v: int) -> Result<int, string> {
///     if v > 3 {
///         return .Ok(42)
///     }
///     return .Error("Wrong number")
/// }
/// ```
/// 
/// Matching result in `match` expression:
/// 
/// ```raven
/// val result = Test(2)
/// 
/// val r = result match {
///     .Ok(val value) => "Ok: $value"
///     .Error(val message) => "Error: '$message'"
/// }
/// ```
///
/// Simple pattern:
///
/// ```raven
/// if result is .Ok(val value) {
///     // Omitted
/// } else if result is .Error(val error) {
///     // Omitted
/// }
/// ```
public union Result<T, E> {
    /// Represents a result value
    Ok(value: T)

    /// Represents an error
    Error(data: E)
}

/// Contains extensions for the Result<T, E> type
public extension ResultExtensions<T, E> for Result<T, E> {

    /// Gets a value indicating whether the Result was Ok
    public HasOk: bool {
        get => self is .Ok(val message)
    }

    /// Gets a value indicating whether the Result was Error
    public HasError: bool {
        get => self is .Error(val message)
    }

    /// Converts the Result to an Option
    public IsOk: Option<T> {
        get {
            if self is .Ok(val v) {
                return .Some(v)
            }

            return .None
        }
    }
    
    /// Unwrap the value if Ok, otherwise return the default value
    public UnwrapOrDefault() -> T {
        if self is .Ok(val value) {
            return value
        }

        return default(T)
    }

    /// Unwrap the value if Ok, otherwise throw an exception
    public UnwrapOrThrow() -> T {
        var error: E = default
        if self is .Ok(val value) {
            return value
        } else if self is .Error(val error2) {
            error = error2
        }

        throw new InvalidOperationException(error.ToString())
    }

    /// Unwrap the value if Ok, otherwise return the specified value
    public UnwrapOr(defaultValue: T) -> T {
        if self is .Ok(val value) {
            return value
        }

        return defaultValue
    }

    /// Unwrap the Error, or throw an exception
    public UnwrapError() -> E {
        if self is .Error(val message) {
            return message
        }

        throw new InvalidOperationException("Result is not Error")
    }
}
