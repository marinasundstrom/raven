import System.*
import System.IO.*
import System.Text.*
import Microsoft.AspNetCore.Builder.*
import Microsoft.AspNetCore.Http.*
import Microsoft.AspNetCore.Mvc.*
import System.Threading.Tasks.*

val builder = WebApplication.CreateBuilder(args)
val app = builder.Build()

app.MapGet("/", () => "Hello from Raven Minimal API")

app.MapGet("/del", (context: HttpContext) => context.Response.WriteAsync("Hello World"))

func ping(name: string) -> PingResult {
    PingResult("pong ${name}")
}

app.MapGet("/ping0", ping)

func ping1(name: string) -> Result<PingResult, CustomError> {
    match name {
        "Bob" | "bob" => .Ok(PingResult("pong ${name}"))
        _ => .Error(CustomError("Invalid name"))
    }
}

app.MapGet("/ping1", ping1)

app.MapGet("/ping", 
    (name: string) => "pong ${name}")

app.MapGet("/ping2", (no: int = 2) => "pong ${no}")

app.MapGet("/async", async () => {
    await Task.Delay(1)
    return "Hello from async MapGet"
})

app.MapPost("/submit", async (context: HttpContext) => {
    use reader = StreamReader(context.Request.Body, Encoding.UTF8, true, 1024, false)
    val content = await reader.ReadToEndAsync()
    return "submitted: $content"
})

app.MapPost("/submit2", (data: Data) => "submitted: ${data.Value}")

app.MapPost("/submit-async", async (content: string) => {
    await Task.Delay(1)
    return "submitted async: $content"
})

app.Run()

public record class Data(Value: string)

public record class PingResult(Message: string)

public record class CustomError(Message: string)