/*
 * Demonstrates discriminated unions in Raven.
 */

import System.*

let res = parseNumber("foo")

Console.WriteLine("Is error: ${res.IsError}")

let str = res match {
    .Ok(v) => "Number is: ${v}"
    .Error(message) => message
}

Console.WriteLine(str)

let res2 = parseNumber("foo").UnwrapOrThrow()

func parseNumber(str: string) -> Result<int> {
    return try int.Parse(str) match {
        int v => .Ok(v)
        Exception exc => .Error(exc.Message)
    }
}

union Result<T> {
    Ok(value: T)
    Error(message: string)
}

union Result<T, E> {
    Ok(value: T)
    Error(data: E)
}

extension ResultExtensions<T> for Result<T> {

    // Arrow prop syntax not working
    //public IsOk: bool => self is .Ok(value)

    public IsOk: bool {
        get => self is .Ok(message)
    }

    public IsError: bool {
        get => self is .Error(message)
    }

    public UnwrapOrDefault() -> T {
        if self is .Ok(value) {
            return value
        }
        return default(T)
    }

    public UnwrapOrThrow() -> T {
        var error: string = ""
        if self is .Ok(value) {
            return value
        } else if self is .Error(error2) {
            error = error2
        }
        throw new System.InvalidOperationException("${error}")
    }
}