import System.*
import System.Console.*
import System.Text.Json.*
import System.Text.Json.Serialization.*

val opt: Option<int> = .Some(42)

val settings = JsonSerializerOptions {
    WriteIndented = true
}

//settings.Converters.Insert(0, OptionJsonConverterFactory())

WriteLine(JsonSerializer.Serialize(opt, settings))

[JsonConverter(typeof(OptionJsonConverterFactory))]
public union Option<T> {
    Some(value: T)
    None
}

public class OptionJsonConverter<T> : JsonConverter<Option<T>> {
    public override Read(reader: &Utf8JsonReader, typeToConvert: Type, options: JsonSerializerOptions) -> Option<T> {
        use doc = JsonDocument.ParseValue(reader)
        val root = doc.RootElement

        val @case = root.GetProperty("case").GetString()

        return @case match {
            "Some" => .Some(
                root.GetProperty("value").Deserialize<T>(options)
            )
            "None" => .None
            _ => .None
        }
    }

    public override Write(writer: Utf8JsonWriter, value: Option<T>, options: JsonSerializerOptions) -> () {
        writer.WriteStartObject()

        if value is .Some(val someValue) {
            writer.WriteString("case", "Some")
            writer.WritePropertyName("value")
            JsonSerializer.Serialize(writer, someValue, options)
        } else if value is .None {
            writer.WriteString("case", "None")
        }

        writer.WriteEndObject()
    }
}

public class OptionJsonConverterFactory : JsonConverterFactory {

    public override CanConvert(typeToConvert: Type) -> bool {
        // Option<T>
        return typeToConvert.IsGenericType
            && typeToConvert.GetGenericTypeDefinition() == typeof(Option<>)
    }

    public override CreateConverter(typeToConvert: Type, options: JsonSerializerOptions) -> JsonConverter {
        val tArg = typeToConvert.GetGenericArguments()[0]

        // OptionJsonConverter<TArg>
        val converterType = typeof(OptionJsonConverter<>).MakeGenericType([tArg])

        // new OptionJsonConverter<TArg>()
        return (JsonConverter)Activator.CreateInstance(converterType)!
    }
}
