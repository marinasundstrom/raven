import System.*
import System.Console.*
import System.Collections.Generic.*
import System.Text.Json.*
import System.IO.*
import System.Threading.Tasks.*

val result = await GetPerson("Derek")
val person: Person? = result match {
    .Ok(val p) => p
    .Error(_) => null
}

Console.WriteLine("Person is: ${person?.Name}")

if person is not null {
    val e = ListProducts(person) match {
        .Ok => ()
        .Error(val message) => Console.WriteLine(message)
    }
}

func ListProducts(person: Person) -> Result<(), string> {
    val products = person.Products

    if products.Count == 0 {
        return .Error("No products")
    }

     for product in products {
        WriteLine(product.Name)
    }   

    return .Ok
}

async func GetPerson(name: string) -> Task<Result<Person, string>> {
    val persons = await GetPersons()
    val ps = persons?
    for person in ps {
        if person is { Name: name } {
            return .Ok(person)
        }
    }
    return .Error("Not found")
}

async func GetPersons() -> Task<Result<Person[], string>> {
    val personResult = await GetPersonsCore()
    return personResult match {
        .Ok(val persons) => .Ok(persons)
        .Error(val exception) => .Error(exception.Message)
    }
}

async func GetPersonsCore() -> Task<Result<Person[], Exception>> {
    val textResult = try await File.ReadAllTextAsync("sandbox/sample.json")
    val results = try JsonSerializer.Deserialize<Person[]>(textResult?)
    return .Ok(results?)
}

class Person {
    public Name: string { get; set; }

    public Products: List<Product> { get; set; }
}

class Product {
    public Name: string { get; set; }
}