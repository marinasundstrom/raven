import System.*
import System.Console.*
import System.Collections.Generic.*
import System.Text.Json.*
import System.Text.Json.Serialization.*

val window = Window {
    Title = "Main"
    Width = 800
    Height = 600

    StackPanel {
        Button(text: "Ok") { 
            HorizontalAlignment = .Fill
        }
        Button(text: "Cancel") { 
            HorizontalAlignment = .Fill 
        }
    }
}

func openDialog(window: Window) -> Result<string, Exception> {
    val result = window.ShowDialog()?
    if result is .Cancel {
        // Bang
        WriteLine("Bang")
        return .Error(InvalidOperationException("BANG!"))
    }
    return .Ok(result.ToString())
}

val result = openDialog(window)

WriteLine(result)

WriteLine(window.Title)

WriteLine(JsonSerializer.Serialize(window, JsonSerializerOptions {
    WriteIndented = true
}))

if window.Content is StackPanel stackPanel {
    for control in stackPanel.Children {
        if control is Button button {
            WriteLine(button.Text)
        }
    }
}

class Window {
    public Title: string { get; set; } = ""

    public Width: int { get; set; } = 250

    public Height: int { get; set; } = 250

    public Content: Control? { get; set; } = null

    public IsVisible: bool { get; private set; } = false

    public Show() -> () {
        IsVisible = true
    }

    public Close() -> () {
        IsVisible = false
    }

    public ShowDialog() -> Result<DialogResult, Exception> {
        return .Ok(DialogResult.Cancel)
    }
}

[JsonConverter(typeof(JsonStringEnumConverter<DialogResult>))]
enum DialogResult { 
    Ok, 
    Cancel, 
    Yes, 
    No 
}

[JsonPolymorphic(TypeDiscriminatorPropertyName: "type")]
[JsonDerivedType(typeof(Button), "Button")]
[JsonDerivedType(typeof(StackPanel), "StackPanel")]
open abstract class Control {}

interface IHasChildren {
    Children: IEnumerable<Control> { get; }

    Add(control: Control) -> ();
}

class StackPanel : Control, IHasChildren {
    val children: List<Control> = []

    public init() {
    }

    public init(orientation: Orientation) {
        Orientation = orientation
    }

    public Orientation: Orientation { get; set; } = .Vertical

    public Children: IEnumerable<Control> => children

    public Add(control: Control) -> () {
        children.Add(control)
    }
}

class Button(text: string) : Control {
    public Text: string { get; set; } = text

    public HorizontalAlignment: HorizontalAlignment { get; set; } = .Fill
}

[JsonConverter(typeof(JsonStringEnumConverter<Orientation>))]
enum Orientation {
    Horizontal,
    Vertical
}

[JsonConverter(typeof(JsonStringEnumConverter<HorizontalAlignment>))]
enum HorizontalAlignment {
    Left,
    Center,
    Right,
    Fill
}
