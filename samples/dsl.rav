import System.*
import System.Console.*
import System.Collections.Generic.*
import System.Text.Json.*

val window = Window {
    Title = "Main"
    Width = 800
    Height = 600

    StackPanel(orientation: .Vertical) {
        Button(text: "Ok") { 
            HorizontalAlignment = .Fill
        }
        Button(text: "Cancel") { 
            HorizontalAlignment = .Fill 
        }
    }
}

func openDialog(window: Window) -> Result<string, Exception> {
    val result = window.ShowDialog()?
    if result is .Cancel {
        // Bang
        WriteLine("Bang")
        return .Error(InvalidOperationException("BANG!"))
    }
    return .Ok(result.ToString())
}

val result = openDialog(window)

WriteLine(result)

WriteLine(window.Title)

WriteLine(JsonSerializer.Serialize(window, JsonSerializerOptions() {
    WriteIndented = true
}))

if window.Content is StackPanel stackPanel {
    for control in stackPanel.Children {
        if control is Button button {
            WriteLine(button.Text)
        }
    }
}

class Window {
    public Title: string { get; set; }

    public Width: int { get; set; }

    public Height: int { get; set; }

    public Content: Control { get; set; }

    public IsVisible: bool { get; private set; }

    public Show() -> () {
        IsVisible = true
    }

    public Close() -> () {
        IsVisible = false
    }

    public ShowDialog() -> Result<DialogResult, Exception> {
        return .Ok(DialogResult.Cancel)
    }
}

enum DialogResult { 
    Ok, 
    Cancel, 
    Yes, 
    No 
}

open abstract class Control {}

interface IHasChildren {
    Children: IEnumerable<Control> { get; }

    Add(control: Control) -> ();
}

class StackPanel : Control, IHasChildren {
    val children: List<Control> = []

    public init() {
    }

    public init(orientation: Orientation) {
        Orientation = orientation
    }

    public Orientation: Orientation { get; set; } = .Vertical

    public Children: IEnumerable<Control> => children

    public Add(control: Control) -> () {
        children.Add(control)
    }
}

class Button(text: string) : Control {
    public Text: string { get; set; } = text

    public HorizontalAlignment: HorizontalAlignment { get; set; } 
}

enum Orientation {
    Horizontal,
    Vertical
}

enum HorizontalAlignment {
    Left,
    Center,
    Right,
    Fill
}